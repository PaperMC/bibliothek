buildscript {
  ext {
    indraVersion = "1.3.1"
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.11.RELEASE"
  id "net.kyori.indra" version "$indraVersion"
  id "net.kyori.indra.checkstyle" version "$indraVersion"
  id "net.kyori.indra.license-header" version "$indraVersion"
  id "org.springframework.boot" version "2.4.5"
  id "java"
}

group "io.papermc"
version "1.0.0-SNAPSHOT"

indra {
  javaVersions {
    target = 11
  }

  github("PaperMC", "bibliothek")
  mitLicense()
}

bootJar {
  launchScript()
}

// From StackOverflow: https://stackoverflow.com/a/53087407
// Licensed under: CC BY-SA 4.0
task buildForDocker(type: Copy){
  from bootJar
  into 'build/libs/docker'
  rename { String fileName ->
    // a simple way is to remove the "-$version" from the jar filename
    // but you can customize the filename replacement rule as you wish.
    fileName.replace("-$project.version", "")
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  sonatypeSnapshots()
}

dependencies {
  annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
  checkstyle("ca.stellardrift:stylecheck:0.1")
  implementation("com.vdurmont:semver4j:3.1.0")
  implementation("net.kyori:coffee-functional:1.0.0-SNAPSHOT")
  implementation("org.springdoc:springdoc-openapi-ui:1.5.10")
  implementation("org.springframework.boot:spring-boot-starter-data-mongodb")
  implementation("org.springframework.boot:spring-boot-starter-undertow")
  implementation("org.springframework.boot:spring-boot-starter-validation")
  implementation("org.springframework.boot:spring-boot-starter-web") {
    exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat" // we use undertow
  }
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }
}
